* PSpice Model Editor - Version 9.2

*$
.SUBCKT IRFZ46N 1 2 3
**************************************
*      Model Generated by MODPEX     *
*Copyright(c) Symmetry Design Systems*
*         All Rights Reserved        *
*    UNPUBLISHED LICENSED SOFTWARE   *
*   Contains Proprietary Information *
*      Which is The Property of      *
*     SYMMETRY OR ITS LICENSORS      *
*Commercial Use or Resale Restricted *
*   by Symmetry License Agreement    *
**************************************
* Model generated on Apr 24, 96
* Model format: SPICE3
* Symmetry POWER MOS Model (Version 1.0)
* External Node Designations
* Node 1 -> Drain
* Node 2 -> Gate
* Node 3 -> Source
M1 9 7 8 8 MM L=100u W=100u
* Default values used in MM:
* The voltage-dependent capacitances are
* not included. Other default values are:
*   RS=0 RD=0 LD=0 CBD=0 CBS=0 CGBO=0
.MODEL MM NMOS LEVEL=1 IS=1e-32 T_ABS={temp_abs}
+VTO=3.82619 LAMBDA=0 KP=36.481
+CGSO=1.3939e-05 CGDO=5.05896e-07
RS 8 3 0.013932
D1 3 1 MD
.MODEL MD D IS=8.99141e-09 RS=0.00661401 N=1.46353 BV=55 T_ABS={temp_abs}
+IBV=0.00025 EG=1 XTI=3.00311 TT=0.0001
+CJO=1.41544e-09 VJ=1.09154 M=0.508083 FC=0.5
RDS 3 1 2.2e+06
RD 9 1 0.0001
RG 2 7 3.53852
D2 4 5 MD1
* Default values used in MD1:
*   RS=0 EG=1.11 XTI=3.0 TT=0
*   BV=infinite IBV=1mA
.MODEL MD1 D IS=1e-32 N=50 T_ABS={temp_abs}
+CJO=1.4611e-09 VJ=0.5 M=0.675133 FC=1e-08
D3 0 5 MD2
* Default values used in MD2:
*   EG=1.11 XTI=3.0 TT=0 CJO=0
*   BV=infinite IBV=1mA
.MODEL MD2 D IS=1e-10 N=0.4 RS=3e-06
RL 5 10 1
FI2 7 9 VFI2 -1
VFI2 4 0 0
EV16 10 0 9 7 1
CAP 11 10 1.83136e-09
FI1 7 9 VFI1 -1
VFI1 11 6 0
RCAP 6 10 1
D4 0 6 MD3
* Default values used in MD3:
*   EG=1.11 XTI=3.0 TT=0 CJO=0
*   RS=0 BV=infinite IBV=1mA
.MODEL MD3 D IS=1e-10 N=0.4 T_ABS={temp_abs}
.ENDS IRFZ46N
*$
*
*********************************************
*
.SUBCKT IRF540 1 2 3
**************************************
*      Model Generated by MODPEX     *
*Copyright(c) Symmetry Design Systems*
*         All Rights Reserved        *
*    UNPUBLISHED LICENSED SOFTWARE   *
*   Contains Proprietary Information *
*      Which is The Property of      *
*     SYMMETRY OR ITS LICENSORS      *
*Commercial Use or Resale Restricted *
*   by Symmetry License Agreement    *
**************************************
* Model generated on Apr 24, 96
* Model format: SPICE3
* Symmetry POWER MOS Model (Version 1.0)
* External Node Designations
* Node 1 -> Drain
* Node 2 -> Gate
* Node 3 -> Source
M1 9 7 8 8 MM L=100u W=100u
* Default values used in MM:
* The voltage-dependent capacitances are
* not included. Other default values are:
*   RS=0 RD=0 LD=0 CBD=0 CBS=0 CGBO=0
.MODEL MM NMOS LEVEL=1 IS=1e-32
+VTO=3.56362 LAMBDA=0.00291031 KP=25.0081
+CGSO=1.60584e-05 CGDO=4.25919e-07
RS 8 3 0.0317085
D1 3 1 MD
.MODEL MD D IS=1.02194e-10 RS=0.00968022 N=1.21527 BV=100 T_ABS={temp_abs}
+IBV=0.00025 EG=1.2 XTI=3.03885 TT=1e-07
+CJO=1.81859e-09 VJ=1.1279 M=0.449161 FC=0.5
RDS 3 1 4e+06
RD 9 1 0.0135649
RG 2 7 5.11362
D2 4 5 MD1
* Default values used in MD1:
*   RS=0 EG=1.11 XTI=3.0 TT=0
*   BV=infinite IBV=1mA
.MODEL MD1 D IS=1e-32 N=50 T_ABS={temp_abs}
+CJO=2.49697e-09 VJ=0.5 M=0.9 FC=1e-08
D3 0 5 MD2
* Default values used in MD2:
*   EG=1.11 XTI=3.0 TT=0 CJO=0
*   BV=infinite IBV=1mA
.MODEL MD2 D IS=1e-10 N=0.4 RS=3e-06
RL 5 10 1
FI2 7 9 VFI2 -1
VFI2 4 0 0
EV16 10 0 9 7 1
CAP 11 10 2.49697e-09
FI1 7 9 VFI1 -1
VFI1 11 6 0
RCAP 6 10 1
D4 0 6 MD3
* Default values used in MD3:
*   EG=1.11 XTI=3.0 TT=0 CJO=0
*   RS=0 BV=infinite IBV=1mA
.MODEL MD3 D IS=1e-10 N=0.4 T_ABS={temp_abs}
.ENDS IRF540
*$
*
***************************
* lib de ICAP 1.8.6 (1998)
* Hitachi  160 Volt  7 Amp  .171 ohm  N-Channel Power MOSFET  08-06-1993
.SUBCKT 2SK1058  10 20 40
*     TERMINALS:  D  G  S
M1   1  2  3  3  DMOS L=1U W=1U
RD  100  1  80.4M
RS  30  3  5.28M
RG  20  2  21.4
CGS  2  3  410P
EGD 12  0  2  1  1
VFB 14  0  0
FFB  2  1  VFB  1
CGD 13 14  128P
R1  13  0  1
D1  12 13  DLIM
DDG 15 14  DCGD
R2  12 15  1
D2  15  0  DLIM
DSD  3 100  DSUB
LS  30 40  7.5N
LD  10 100 4N
.MODEL DMOS NMOS (LEVEL=3 THETA=85M VMAX=163K ETA=2.2M VTO=.2 KP=.999 T_ABS={temp_abs})
.MODEL DCGD D (CJO=128P VJ=.6 M=.68 T_ABS={temp_abs})
.MODEL DSUB D (IS=29N N=1.5 RS=61.4M BV=160 CJO=802P VJ=.8 M=.42 TT=252N T_ABS={temp_abs})
.MODEL DLIM D (IS=100U T_ABS={temp_abs})
.ENDS 2SK1058 
*$
*
**********************************
* qlq modif pour compatibilite avec PSPICE v8
* modelisation de R avec TC1 et TC2
* (c) Loutre Noel juin 2001
* MOSFETs N /Harris RFP50N06
* 0,022 Ohms : 
.SUBCKT RFP50N06 2 1 3 
Ca 12 8 3.68e-9
Cb 15 14 3.625e-9
Cin 6 8 1.98e-9
Dbody 7 5 DBDMOD
Dbreak 5 11 DBKMOD
Dplcap 10 5 DPLCAPMOD
Ebreak 11 7 17 18 64.59
Eds 14 8 5 8 1
Egs 13 8 6 8 1
Esg 6 10 6 8 1
Evto 20 6 18 8 1
It 8 17 1
Ldrain 2 5 1e-9
Lgate 1 9 5.65e-9
Lsource 3 7 4.13e-9
Mos1 16 6 8 8 MOSMOD L=.99U W=.99U ; M=0.99
Mos2 16 21 8 8 MOSMOD L=.01U W=.01U ; M=0.01
* Rbreak 17 18  RBKMOD 1
Rbreak 17 18  1  TC=1.23e-3,-2.34e-6  
* Rdrain 5 16 RDSMOD 1.00e-4
Rdrain 5 16  1.00e-4 TC=5.01e-3,1.49e-5 
Rgate 9 20 0.690
Rin 6 8 1e9
* Rsource 8 7 RDSMOD 12.0e-3
Rsource 8 7 12.0e-3 TC=5.01e-3,1.49e-5 
* Rvto 18 19 RVTOMOD 1
Rvto 18 19  1  TC=-5.03e-3,-5.16e-6
S1a 6 12 13 8 S1AMOD
S1b 13 12 13 8 S1BMOD
S2a 6 15 14 13 S2AMOD
S2b 13 15 14 13 S2BMOD
Vbat 8 19 DC 1
Vto 21 6 0.678
.MODEL DBDMOD D (IS=9.851e-13 RS=4.91e-3 T_ABS={temp_abs} 
+ CJO=2.05e-9 TT=4.33e-8) ; TRS1=2.07e-3 TRS2=2.51e-7)
.MODEL DBKMOD D (RS=1.98e-1 T_ABS={temp_abs}) ; TRS1=-2.35e-3 TRS2=-3.83e-6
.MODEL DPLCAPMOD D (CJO=1.42e-9 IS=1e-30 N=10 T_ABS={temp_abs})
.MODEL MOSMOD NMOS (VTO=3.65 KP=35 IS=1e-30 TOX=1 T_ABS={temp_abs})
*.MODEL RBKMOD R (TC1=1.23e-3 TC2=-2.34e-6)
*.MODEL RDSMOD R (TC1=5.01e-3 TC2=1.49e-5)
*.MODEL RVTOMOD R (TC1=-5.03e-3 TC2=-5.16e-6)
.MODEL S1AMOD VSWITCH(RON=1e-5 ROFF=0.1 VON=-6.75 VOFF=-2.50)
.MODEL S1BMOD VSWITCH(RON=1e-5 ROFF=0.1 VON=-2.50 VOFF=-6.75)
.MODEL S2AMOD VSWITCH(RON=1e-5 ROFF=0.1 VON=-2.7 VOFF=2.3)
.MODEL S2BMOD VSWITCH(RON=1e-5 ROFF=0.1 VON=2.3 VOFF=-2.7)
.ENDS RFP50N06
*$
*
.model 1N4148   D(Is=2.682n N=1.836 Rs=.5664 Ikf=44.17m Xti=3 Eg=1.11 Cjo=4p
+               M=.3333 Vj=.5 Fc=.5 Isr=1.565n Nr=2 Bv=100 Ibv=100u Tt=11.54n)
*
*******************
*$
.model  1N4007  D(Is=14.11n N=1.984 Rs=33.89m Ikf=94.81 Xti=3 Eg=1.11 T_ABS=40
+               Cjo=25.89p M=.44 Vj=.3245 Fc=.5 Bv=1500 Ibv=10u Tt=5.7u)
*               Motorola
*               Semiconductor Databook (mid 1970s)
*               03 Jun 91       pwt     creation
*********************************************
*
*$
.MODEL 1N1184 D (IS=23.4N RS=1.75M N=1.61 BV=133.3 IBV=4M
+ CJO=4.4N VJ=.75 M=.333 TT=144U T_ABS=50)
* Motorola  100 Volt  20 Amp  100 us  Si Diode  11-23-1990
*
*****************
*$
.model 1N1190  D(Is=1.331u N=2.108 Rs=1.029m
+ Ikf=11.73 Xti=3 Eg=1.11 Cjo=1p M=.5 Vj=.7 Fc=.5
+ Isr=40.74u Nr=2 Bv=400 Ibv=100u Tt=5n
+ T_ABS=50)
*
*
***************************************
* macro-modele TIP 141 Darlington NPN
*(ICmax=10A:Pmax=125W:BETAmin=500:VCEomax=80V)  
*$
.SUBCKT TIP141   1 2 3
*  connections:  C B E
Q1 1 2 4 QTIP141   0.1
Q2 1 4 3 QTIP141
R1 2 4  8K
R2 4 3  40
D1 3 1  DTIP141
.MODEL QTIP141 NPN (IS=12P NF=1 BF=99.9 VAF=161 IKF=8 ISE=981P NE=2
+ BR=4 NR=1 VAR=20 IKR=12 RE=40M RB=.16 RC=16M XTB=1.5 T_ABS={temp_abs}  
+ CJE=1.34N VJE=.74 MJE=.45 CJC=193P VJC=1.1 MJC=.24 TF=98.5N TR=4.25U)
.MODEL DTIP141 D (IS=12P N=1 RS=40M BV=80 IBV=.001 CJO=193P TT=4.25U T_ABS={temp_abs})      
.ENDS TIP141
*$
************************
*TIP142;BJTs NPN;Darlington;100V 10A
*
.SUBCKT TIP142   1 2 3
*    TERMINALS:  C B E
*  100 Volt  10 Amp  NPN Darlington Transistor  08-04-1995
Q1 1 2 4 QPWR .1
Q2 1 4 3 QPWR
R1 2 4  8K
R2 4 3  40
D1 3 1  DSUB
.MODEL QPWR NPN (IS=12P NF=1 BF=150 VAF=180 IKF=4.64 ISE=494P NE=2
+ BR=4 NR=1 VAR=20 IKR=6.96 RE=.115 RB=.46 RC=46M XTB=1.5 T_ABS={temp_abs}
+ CJE=1.34N VJE=.74 MJE=.45 CJC=193P VJC=1.1 MJC=.24 TF=110N TR=4.75U)
.MODEL DSUB D (IS=12P N=1 RS=.115 BV=100 IBV=.001 CJO=193P TT=4.75U T_ABS={temp_abs})
.ENDS TIP142
*$
*
* OPA627E OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* "E" IS ENHANCED MODEL
* CREATED USING PARTS RELEASE 4.03 ON 10/04/90 AT 09:30
*
* REV.B  5/22/91 TK
* REV.C  3/21/92 BCB: added input bias current correction and 
*                     current and voltage noise
*
*  ------------------------------------------------------------------------ 
* |  NOTICE: THE INFORMATION PROVIDED HEREIN IS BELIEVED TO BE RELIABLE;   |
* |  HOWEVER; BURR-BROWN ASSUMES NO RESPONSIBILITY FOR INACCURACIES OR     |
* |  OMISSIONS.  BURR-BROWN ASSUMES NO RESPONSIBILITY FOR THE USE OF THIS  |
* |  INFORMATION, AND ALL USE OF SUCH INFORMATION SHALL BE ENTIRELY AT     |
* |  THE USER'S OWN RISK.  NO PATENT RIGHTS OR LICENSES TO ANY OF THE      |
* |  CIRCUITS DESCRIBED HEREIN ARE IMPLIED OR GRANTED TO ANY THIRD PARTY.  |
* |  BURR-BROWN DOES NOT AUTHORIZE OR WARRANT ANY BURR-BROWN PRODUCT FOR   |
* |  USE IN LIFE-SUPPORT DEVICES AND/OR SYSTEMS.                           |
*  ------------------------------------------------------------------------ 
*
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT OPA627   1 2 3 4 5
*
C1   11 12 3.182E-12
C2    6  7 23.00E-12
CSS  10 99 40.45E-12
DC    5 53 DX
DE   54  5 DX
DLP  90 91 DX
DLN  92 90 DX
DP    4  3 DX
EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
FB    7 99 POLY(5) VB VC VE VLP VLN 0 419.4E6 -40E6 40E6 40E6 -40E6
GA    6  0 11 12 2.432E-3
GCM   0  6 10 99 3.779E-9
ISS   3 10 DC 1.840E-3
HLIM 90  0 VLIM 1K
J1   11  2 10 JX
J2   12  64 10 JX
G11 2 4 POLY(4) (10,2) (11,2) (4,2) (66,0) 0 1E-12 1E-12 1E-12 1.6E-6
G21 1 4 POLY(4) (10,1) (12,1) (4,1) (68,0) 0 1E-12 1E-12 1E-12 1.6E-6
R2    6  9 100.0E3
RD1   4 11 419.4
RD2   4 12 419.4
RO1   8  5 54
RO2   7 99 1
*  RP    3  4 4.286E3
RSS  10 99 108.7E3
VB    9  0 DC 0
VC    3 53 DC 2.700
VE   54  4 DC 2.700
VLIM  7  8 DC 0
VLP  91  0 DC 55
VLN   0 92 DC 55
****************************
* OPA627 "E" - ENHANCEMENTS
****************************
* OUTPUT SUPPLY MIRROR
FQ3   0 20 POLY(1) VLIM 0  1
DQ1  20 21 DX
DQ2  22 20 DX
VQ1  21  0 0
VQ2  22  0 0
FQ1   3  0 POLY(1) VQ1  4.76E-3  1
FQ2   0  4 POLY(1) VQ2  4.76E-3 -1
* QUIESCIENT CURRENT
RQ    3  4  7.5E4
* DIFF INPUT CAPACITANCE
CDIF  1  2  8.0E-12
* COMMON MODE INPUT CAPACITANCE
C1CM  1  99 7.0E-12
C2CM  2  99 7.0E-12
* INPUT VOLTAGE NOISE
VN1 61 0  0.6
VN2 0  62 0.6
DN1 61 63 DY
DN2 63 62 DY
EN 64 1  63 0 1
* INPUT CURRENT NOISE
RN1 0 65 60.3865
RN2 65 66 60.3865
RN3 66 0 120.773
RN4 0 67 60.3865
RN5 67 68 60.3865
RN6 68 0 120.773
******************
.MODEL DY D(IS=1E-12 AF=1 KF=71.89E-15)
.MODEL DX D(IS=800.0E-18)
.MODEL JX PJF(IS=500.0E-15 BETA=1.545E-3 VTO=-1)
.ENDS OPA627
*$
****************************************
*
****** TL071 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT **********
*
* CREATED USING PARTS RELEASE 4.01 ON 06/16/89 AT 13:08
* (REV N/A)      SUPPLY VOLTAGE: +/-15V
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT TL07x    1 2 3 4 5
*
  C1   11 12 3.498E-12
  C2    6  7 15.00E-12
  DC    5 53 DX
  DE   54  5 DX
  DLP  90 91 DX
  DLN  92 90 DX
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 4.715E6 -5E6 5E6 5E6 -5E6
  GA    6  0 11 12 282.8E-6
  GCM   0  6 10 99 8.942E-9
  ISS   3 10 DC 195.0E-6
  HLIM 90  0 VLIM 1K
  J1   11  2 10 JX
  J2   12  1 10 JX
  R2    6  9 100.0E3
  RD1   4 11 3.536E3
  RD2   4 12 3.536E3
  RO1   8  5 150
  RO2   7 99 150
  RP    3  4 2.143E3
  RSS  10 99 1.026E6
  VB    9  0 DC 0
  VC    3 53 DC 2.200
  VE   54  4 DC 2.200
  VLIM  7  8 DC 0
  VLP  91  0 DC 25
  VLN   0 92 DC 25
.MODEL DX D(IS=800.0E-18)
.MODEL JX PJF(IS=15.00E-12 BETA=270.1E-6 VTO=-1)
.ENDS TL07x 
*$
*
*************************
* OP-27G OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* CREATED USING PARTS RELEASE 4.01 ON 06/26/89 AT 12:59
* REV (N/A)
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT OP27     1 2 3 4 5
*
  C1   11 12 2.730E-12
  C2    6  7 15.00E-12
  DC    5 53 DX
  DE   54  5 DX
  DLP  90 91 DX
  DLN  92 90 DX
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 22.86E6 -20E6 20E6 20E6 -20E6
  GA 6  0 11 12 852.1E-6
  GCM   0  6 10 99 754.1E-12
  IEE  10  4 DC 42.03E-6
  HLIM 90  0 VLIM 1K
  Q1   11  2 13 QX
  Q2   12  1 14 QX
  R2    6  9 100.0E3
  RC1   3 11 1.326E3
  RC2   3 12 1.326E3
  RE1  13 10 94.44
  RE2  14 10 94.44
  REE  10 99 4.759E6
  RO1   8  5 88
  RO2   7 99 87
  RP    3  4 10.14E3
  VB    9  0 DC 0
  VC    3 53 DC 2.200
  VE   54  4 DC 2.200
  VLIM  7  8 DC 0
  VLP  91  0 DC 40
  VLN   0 92 DC 40
.MODEL DX D(IS=800.0E-18)
.MODEL QX NPN(IS=800.0E-18 BF=1.400E3)
.ENDS OP27
*$
*
* connections:   non-inverting input
*                | inverting input
*                | | positive power supply
*                | | | negative power supply
*                | | | | open collector output
*                | | | | |
.SUBCKT LM339    1 2 3 4 5
*
  f1    9  3 v1 1
  iee   3  7 dc 100.0E-6
  vi1  21  1 dc .75
  vi2  22  2 dc .75
  q1    9 21  7 qin
  q2    8 22  7 qin
  q3    9  8  4 qmo
  q4    8  8  4 qmi
.model qin PNP(Is=800.0E-18 Bf=2.000E3)
.model qmi NPN(Is=800.0E-18 Bf=1002)
.model qmo NPN(Is=800.0E-18 Bf=1000 Cjc=1E-15 Tr=475.4E-9)
  e1   10  4  9  4  1
  v1   10 11 dc 0
  q5    5 11  4 qoc
.model qoc NPN(Is=800.0E-18 Bf=20.69E3 Cjc=1E-15 Tf=3.540E-9 Tr=472.8E-9)
  dp    4  3 dx
  rp    3  4 37.50E3
.model dx  D(Is=800.0E-18 Rs=1)
*
.ENDS LM339
*$

**********************************
* ADG406 SPICE Macro-model                10/95, Rev. D
*                                           WF / ADSC
*
* Revision History: NONE
*
*       NOTE: This model was setup with typical leakage currents
*               at +25C for ADG406 
*
* Copyright 1995 by Analog Devices, Inc.
*
* Refer to "README.DOC" file for License Statement.  Use of this 
* model indicates your acceptance with the terms and provisions 
* in the License Statement.
*
* Node assignments
* 19 - S1, 20 - S2, 21 - S3, 22 - S4, 23 - S5, 24 - S6, 25 - S7
* 26 - S8, 11 - S9,10 - S10, 9 - S11, 8 - S12, 7 - S13, 6 - S14
* 5 - S15, 4 - S16, 14 - A3, 15 - A2, 16 - A1, 17 - A0, 18 - EN
* 28 - D, 1 - VDD, 12 - GND, 27 - VSS
*                    
.SUBCKT ADG406 19 20 21 22 23 24 25 26 11 10 9 8 7 6 5 4 14 15 16 17 18 28 1 12 27
*
* DEMUX SWITCHES (S1-16 ---> D)
*
*     First Section is for control line A0
*          All nodes in this section are in the 30's unless
*          they are I/O nodes
*
E_A0_2    200 12  17 12    1
E_A0_1     30 40 201 40   -1
R_A0_1    200 201         1000
C_A0_X2   201 12          100E-12

V_AX_1     40  12          1.6

S_A0_16     4 31 201 12   Sdemux
S_A0_15     5 31 30  12   Sdemux                
S_A0_14     6 32 201 12   Sdemux
S_A0_13     7 32 30  12   Sdemux
S_A0_12     8 33 201 12   Sdemux
S_A0_11     9 33 30  12   Sdemux
S_A0_10    10 34 201 12   Sdemux
S_A0_9     11 34 30  12   Sdemux
S_A0_8     26 35 201 12   Sdemux
S_A0_7     25 35 30  12   Sdemux                
S_A0_6     24 36 201 12   Sdemux
S_A0_5     23 36 30  12   Sdemux
S_A0_4     22 37 201 12   Sdemux
S_A0_3     21 37 30  12   Sdemux
S_A0_2     20 38 201 12   Sdemux
S_A0_1     19 38 30  12   Sdemux

C_A0_X1     17 12          4E-12
C_A0_D      17 39          4E-12

*          Input capacitances

C_A0_1     19  12          8E-12
C_A0_2     20  12         8E-12
C_A0_3     21  12         8E-12
C_A0_4     22  12         8E-12
C_A0_5     23  12         8E-12
C_A0_6     24  12         8E-12
C_A0_7     25  12         8E-12
C_A0_8     26  12         8E-12
C_A0_9     11  12         8E-12
C_A0_10    10  12         8E-12
C_A0_11     9  12         8E-12
C_A0_12     8  12         8E-12
C_A0_13     7  12         8E-12
C_A0_14     6  12         8E-12
C_A0_15     5  12         8E-12
C_A0_16     4  12         8E-12

C_D_1      28  12         80E-12

*
*       Leakage Current (SX and D ON only) 
*

R_ON_S1    19 500    1.5E12
R_ON_S2    20 500    1.5E12
R_ON_S3    21 500    1.5E12
R_ON_S4    22 500    1.5E12
R_ON_S5    23 500    1.5E12
R_ON_S6    24 500    1.5E12
R_ON_S7    25 500    1.5E12
R_ON_S8    26 500    1.5E12
R_ON_S9    11 500    1.5E12
R_ON_S10   10 500    1.5E12
R_ON_S11    9 500    1.5E12
R_ON_S12    8 500    1.5E12
R_ON_S13    7 500    1.5E12
R_ON_S14    6 500    1.5E12
R_ON_S15    5 500    1.5E12
R_ON_S16    4 500    1.5E12
SLEAK_ON_D 28 500    28 500  SLEAK
ILEAK_ON_D 28 500   -10E-12               
*       Leakage Current (SX OFF only)
*
*       Leakage Current (D OFF only)
*

S_OFF_D     28  58 80 12          Sdemux
R_OFF_D     58  12                1E12 
G_OFF_D     28  12 58 12          0.75E-12

*C_OFF_D     58  12                0E-12   
*
*     Second Section is for control line A1
*

E_A1_2    170 12 16  12    1
E_A1_1     41 40 171 40   -1
R_A1_1    170 171         1000
C_A1_X2   171 12          100E-12

S_A1_1     31 42 171 12     Sdemux
S_A1_2     32 42 41  12     Sdemux
S_A1_3     33 43 171 12    Sdemux
S_A1_4     34 43 41  12    Sdemux
S_A1_5     35 44 171 12    Sdemux
S_A1_6     36 44 41  12    Sdemux
S_A1_7     37 45 171 12    Sdemux
S_A1_8     38 45 41  12    Sdemux

C_A1_X     16 12           4E-12
C_A1_D     16 39           4E-12

*
*     Third Section is for control line A2
*

E_A2_2    160 12 15  12    1
E_A2_1     46 40 161 40   -1
R_A2_1    160 161         1000
C_A2_X2   161  12          100E-12

S_A2_1     42 47 161 12     Sdemux
S_A2_2     43 47 46  12    Sdemux
S_A2_3     44 48 161 12    Sdemux
S_A2_4     45 48 46  12    Sdemux

C_A2_X     15 12           4E-12
C_A2_D     15 39           4E-12

*
*     Forth Section is for control line A3
*

E_A3_2    140  12  14 12   1
E_A3_1     49  40 141 40   -1
R_A3_1    140 141         1000
C_A3_X2   141  12          100E-12

S_A3_1     47 39 141 12     Sdemux
S_A3_2     48 39 49  12    Sdemux

C_A3_X     14 12           4E-12
C_A3_D     14 39           4E-12

*
*     Main Series Switch combination
*
*

V_1_A     419  12  15
V_1_B     420  12  -15
V_1_C     421  500 -0.5         ;sets pos main offset
V_1_D     422  27  2.5          ;sets neg main offset
R_1_C     39   0   1E13
S_1_A     425  39  420 73 SNCM
R_1_A    412 425  24            ;sets neg at max d
S_1_B      426  39  419 73 SPCM
R_1_B     73 426  30            ;sets pos at max d
S_1_C      73 412 611  12 SMAINP
S_1_D     412 411  12 612 SMAINN
E_1_E     611  12         VALUE = {(10*V(73,421))/(0.5*V(1,500)+0.005)}
E_1_F     612  12         VALUE = {(10*V(73,500))/(PWR(V(500,422),1)+0.005)}
S_1_G     411  39   1  27 SBASE

I_XX_1    99 0 0       ;add a 'do-nothing' component
I_XX_2   421 0 0      ;  at nodes 99, 421
I_XX_3   422 0 0      ;    and 422 to keep SPICE  happy

*
*       Voltage Clamp 
*

D_1_POS     39  1    DClamp
G_1_POS     39  1  39  1  -1E-12
D_2_NEG     27  39   DClamp
G_2_NEG     27  39 27  39 1E-12

*     Enable Switch section
*

S_EN_1     73 28 18  12     Sdemux
C_EN_1     18 28           4.2E-12  ; SETS CHARGE INJECTION 

*     Invert Enable Switch section

E_EN0_1     18  81 80 12    -2
V_EN0_1     82  12          2.5
R_EN0_1     82  81         1
R_EN0_2     80  12          1E12

*
*     Power Supply Current Correction
*
I_PS_1      1  12           80E-6
I_PS_2     12  27           0.0001E-6
E_PS_1     99  12  1  12    1
E_PS_2    500  27  1  27    .5

*
*       Crosstalk 
*

RXT_1     19 52           1E13
RXT_2     20 52           1E13
RXT_3     21 52           1E13
RXT_4     22 52           1E13
RXT_5     23 52           1E13
RXT_6     24 52           1E13
RXT_7     25 52           1E13
RXT_8     26 52           1E13
RXT_9     11 52           1E13
RXT_10    10 52           1E13
RXT_11     9 52           1E13
RXT_12     8 52           1E13
RXT_13     7 52           1E13
RXT_14     6 52           1E13
RXT_15     5 52           1E13
RXT_16     4 52           1E13

CXT_1     19 52           1E-12
CXT_2     20 52           1E-12
CXT_3     21 52           1E-12
CXT_4     22 52           1E-12
CXT_5     23 52           1E-12
CXT_6     24 52           1E-12
CXT_7     25 52           1E-12
CXT_8     26 52           1E-12
CXT_9     11 52           1E-12
CXT_10    10 52           1E-12
CXT_11     9 52           1E-12
CXT_12     8 52           1E-12
CXT_13     7 52           1E-12
CXT_14     6 52           1E-12
CXT_15     5 52           1E-12
CXT_16     4 52           1E-12

*
*       OFF Isolation
*
COI_1     19 28           1E-13
COI_2     20 28           1E-13
COI_3     21 28           1E-13
COI_4     22 28           1E-13
COI_5     23 28           1E-13
COI_6     24 28           1E-13
COI_7     25 28           1E-13
COI_8     26 28           1E-13
COI_9     11 28           1E-13
COI_10    10 28           1E-13
COI_11     9 28           1E-13
COI_12     8 28           1E-13
COI_13     7 28           1E-13
COI_14     6 28           1E-13
COI_15     5 28           1E-13
COI_16     4 28           1E-13
ROI_1     19 1901         1.6E9
COI_1A    1901 28         10E-12
ROI_2     20 1902         1.6E9
COI_2A    1902 28         10E-12
ROI_3     21 1903         1.6E9
COI_3A    1903 28         10E-12
ROI_4     22 1904         1.6E9
COI_4A    1904 28         10E-12
ROI_5     23 1905         1.6E9
COI_5A    1905 28         10E-12
ROI_6     24 1906         1.6E9
COI_6A    1906 28         10E-12
ROI_7     25 1907         1.6E9
COI_7A    1907 28         10E-12
ROI_8     26 1908         1.6E9
COI_8A    1908 28         10E-12
ROI_9     11 1909         1.6E9
COI_9A    1909 28         10E-12
ROI_10     10 1910         1.6E9
COI_10A    1910 28         10E-12
ROI_11     9 1911         1.6E9
COI_11A    1911 28         10E-12
ROI_12     8 1912         1.6E9
COI_12A    1912 28         10E-12
ROI_13     7 1913         1.6E9
COI_13A    1913 28         10E-12
ROI_14     6 1914         1.6E9
COI_14A    1914 28         10E-12
ROI_15     5 1915         1.6E9
COI_15A    1915 28         10E-12
ROI_16     4 1916         1.6E9
COI_16A    1916 28         10E-12
*
* MODELS USED
*
.MODEL SNCM  VSWITCH (RON=1 ROFF=500001 VON=9 VOFF=-44)
.MODEL SPCM  VSWITCH (RON=450000 ROFF=1 VON=41 VOFF=-6.5)
.MODEL SBASE VSWITCH (RON=11 ROFF=3500 VON=28 VOFF=-10)
.MODEL SMAINP VSWITCH (RON=650001 ROFF=19 VON=32 VOFF=0)
.MODEL SMAINN VSWITCH (RON=700001 ROFF=7 VON=28.5 VOFF=0)
.MODEL Sdemux VSWITCH (RON=1 ROFF=1E12 VON=2.0 VOFF=1.4)
.MODEL DClamp D(IS=1E-15 IBV=1E-13)
.MODEL SLEAK VSWITCH (RON=750E9 ROFF=150E9 VON=-15 VOFF=15)
.ENDS ADG406
*$
*
******************************
* Acquisition d'un signal analogique
* par Echantillonnage de celui-ci 
* a une frequence = fech 
* (c) LOUTRE Noel mars 1999
*
* macro-modele de l'echantillonneur bloqueur
* d'ordre Zero
*
.SUBCKT echant e Sech PARAMS: fech=352.8k
*
* Interupteur d'acquisition de e  
Sechant e s1 c 0 Inter-ech
.MODEL Inter-ech VSWITCH (Von=3V Voff=2V Ron=0.1 Roff=10E6)
*
* signal de commande de l'inter d'acquisition de e
Vc c 0 PULSE (0 5 0 {1/(500*fech)} {1/(500*fech)}
+             {1/(500*fech)} {1/fech})
*
* memorisation de la tension acquise dans C1
* R1 elimine parasite transitoire lors de la simulation
* 5*R1*C1 < a la fenetre d'acquisition (pw)
R1 s1 s2 {1/(5E-6*fech)}
C1 s2 0 1nF
*
* etage tampon de sortie Gain=1
Eunite Sech 0 VALUE={V(s2)}
.ENDS echant
*$
*
*****************************
* 
* Standard Linear ICs Macromodels, 1993
* Timer
* 1 NEGATIVE POWER SUPPLY
* 2 TRIGGER
* 3 OUTPUT
* 4 RESET
* 5 CONTROL VOLTAGE
* 6 THRESHOLD
* 7 DISCHARGE
* 8 POSITIVE POWER SUPPLY
* 9 ADJUST & INITIALIZATION
*10 ADJUST
*
.SUBCKT ICM7555 1 2 3 4 5 6 7 8 9 10
* data sheet SGS THOMSON / STMicroelectronics TS555
.MODEL MPJR  PMOS VTO=0.0  TOX=3000E-09 KP=2.0E-02 LEVEL=3
.MODEL MNJR  NMOS VTO=0.0  TOX=3000E-09 KP=2.0E-02 LEVEL=3
.MODEL MNDIS NMOS VTO=1.0  TOX=80E-09   KP=2.0E-02 LEVEL=3
.MODEL MDTH D N=0.7  IS=1E-08
*
RAA 8 5 1E+5
RBB 5 27 1E+5
RCC 27 1 1E+5
IALIM 8 1 94E-06
EALIM 24 1 8 1 1
*  COMPA 1 *
E1H 24 41 24 4 1
D1H 32 41 MDTH
D1B  1 32 MDTH
I1 6 1 10P
G1 1 32 5 6 6E-07
* COMPA 2 *
E2H 24 42 24 4 1
D2H 22 42 MDTH
D2B  1 22 MDTH
I2 2 1 10P
G2 1 22 27 2 6E-07
* R S *
MPH 31 32 24 24 MPJR W=50U  L=6U
MNB 31 22  1  1 MNJR W=100U L=6U
MP1  9 31 24 24 MPJR W=42U  L=6U
MN1  9 31  1  1 MNJR W=26U  L=6U
MP2 31  9 24 24 MPJR W=10U  L=37U
MN2 31  9  1  1 MNJR W=9U   L=40U
* RESET *
RRST 4 1 1E+12
* OUTPUT STAGE *
EOUT 15 1 9 1 1
ROUT 15 3 100
RCHOUT 3 1 1E+12
EMI 10 1 24 1 0.5
EGRILLE 11 1 24 9 1
MDIS 13 11 1 1 MNDIS W=4500U  L=6U
RDIS 13 7 20
RCHDIS 7 1 1E+12
.ENDS ICM7555
*$
*
**********************************
*
* Librairie des modeles utilises pour l'ampli Medium a tube
* kit VERDIER modele 210 
* ---------------------------------------------------------
* .SUBCKT ECC83 A G K H1 H2 TRIODE
* .SUBCKT ECC81 A G K H1 H2 TRIODE
* .SUBCKT EL84 A S G K
* .SUBCKT Transfo_EL84 1 2 3 4 5 6
* .SUBCKT Transfo_220 1 2 3 4 5 6 7
*
**********************************************************************
* Duncan Amplfication Generic Triode Model (PSpice Implementation)
* Copyright (C)1997-1998 Duncan Amplfication
* Unauthorised Commercial use prohibited
* Please refer to documentation at http://www.duncanamps.simplenet.com
*
* $MODEL_VERSION$ 3.1
* $AUTOGENERATED$ 15/08/98 11:35:13 
**********************************************************************
* CURRENT MODEL SET AND VERSIONS:
*
*       12AT7 / ECC81       [3.0, 30/12/97 ]
*       12AX7 / ECC83       [3.0, 30/12/97 ]
**********************************************************************
* CORE MODEL VERSION HISTORY:
*
*       1.0   23/09/97      Initial model
*       1.1   19/11/97      Model altered for 1 gig resistors between each node and ground
*       2.0   18/12/97      Two new parameters, ERP and ERI added
*       3.0   30/12/97      New parameter added, RAS
*       3.1   05/01/98      ERI parameter removed
*       3.2   06/01/98      Fix errors in Pspice model
*       3.3   13/01/98      Fixed errors with CDO parameter
*       3.4   25/01/98      Errors with heater versions using ERI  fixed
**********************************************************************
.SUBCKT TRIODE A G K H1 H2
+PARAMS: RCO=1.6 RHO=10.5 HTV=6.3 HWU=10.5
+ LIP=1 LIF=3.7E-3 RAF=18E-3 RAS=1 CDO=0 RAP=4E-3
+ ERP=1.5 
+ MU0=17.3 MUR=19E-3 EMC=9.6E-6 GCO=0 GCF=213E-6
+ CGA=3.9p CGK=2.4p CAK=0.7p 

************************************************************************
*
* Heater model
*
* Can be operated from AC or DC power sources.
* NB: When operating from DC power sources, "Skip initial transient
*     solution" must be checked, to make use of this model.
*
* PARAMETERS
*
*	RCO Heater resistance cold (ohms)
*	RHO Heater resistance hot (ohms)
*	HTV Normal heater voltage (V)
* 	HWU Heater time to warm up to 90% of emission (seconds)
*
************************************************************************

Rcool H1 HA {RCO}
Rload HA HB 1M
Esens HD 0  VALUE={V(HA,HB)*1000}
Epwr  HE 0  VALUE={V(H1,H2)*V(HD)/(PWR({HTV},2)/{RHO})}
RH1   HE HF 91k
CH1   HF 0  {HWU/1E6}
EH2   HG 0  VALUE={V(HF)}
RH2   HG HH 270k
CH2   HH 0  {HWU/1E6}
EH3   HJ 0  VALUE={LIMIT{V(HH)-0.75,0,1E6}*4}
RH3   HJ HK 91k
CH3   HK 0  {HWU/1E6}
Ghot  HB H2 VALUE={(1/(V(HG)+0.001))/({RHO}-{RCO})*V(HB,H2)}

************************************************************************
*
* Anode/grid model
*
* Models reduction in mu at large negative grid voltages
* Models change in Ra with negative grid voltages
* Models limit in Ia with high +Vg and low Va
*
* PARAMETERS
*
*	LIP Conduction limit exponent
*	LIF Conduction limit factor
*	CDO Conduction offset
*	RAF Anode resistance factor for neg grid voltages
*	RAP Anode resistance factor for positive grid voltages
*	ERP Emission power
*	MU0 Mu between grid and anode at Vg=0
*	MUR Mu reduction factor for large negative grid voltages
*	EMC Emission coefficient
*	GCO Grid current offset in volts
*	GCF Grid current scale factor
*
************************************************************************

Elim  LI 0  VALUE={PWR(LIMIT{V(A,K),0,1E6},{LIP})*{LIF}}
Egg   GG 0  VALUE={V(G,K)-{CDO}}
Erpf  RP 0  VALUE={1-PWR(LIMIT{-V(GG)*{RAF},0,0.999},{RAS})+LIMIT{V(GG),0,1E6}*{RAP}}
Egr   GR 0  VALUE={LIMIT{V(GG),0,1E6}+LIMIT{(V(GG))*(1+V(GG)*{MUR}),0,-1E6}}
Eem   EM 0  VALUE={LIMIT{V(A,K)+V(GR)*{MU0},0,1E6}}
Eep   EP 0  VALUE={PWR(V(EM),ERP)*{EMC}*V(RP)}
Eel   EL 0  VALUE={LIMIT{V(EP),0,V(LI)}}
Eld   LD 0  VALUE={LIMIT{V(EP)-V(LI),0,1E6}}
Ga    A  K  VALUE={V(HK)*V(EL)}

************************************************************************
*
* Grid current model
*
* Models grid current, along with rise in grid current at low Va
*
************************************************************************
Egf   GF 0  VALUE={PWR(LIMIT{V(G,K)-{GCO},0,1E6},1.5)*{GCF}}
Gg    G  K  VALUE={(V(GF)+V(LD))*V(HK)}

*
* Capacitances and anti-float resistors
*
CM1	G	K	{CGK}
CM2	A	G	{CGA}
CM3	A	K	{CAK}
RF1	A	0	1000MEG
RF2	G	0	1000MEG
RF3	K	0	1000MEG
*
.ENDS TRIODE
*$
*
*
**********************************************************************
* GENERIC: 12AT7 / ECC81
* MODEL:   12AT7
* NOTES:   Heater model for one half of heater (6.3V)
**********************************************************************
.SUBCKT ECC81 A G K H1 H2
XV1 A G K H1 H2 TRIODE
+PARAMS: RCO= 6.2 RHO= 42 HTV= 6.3 HWU= 10.5 
+ LIP= 1 LIF= 0.0037 RAF= 0.09869 RAS= 1 CDO=-0.5 
+ RAP= 0.1 ERP= 1.4 
+ MU0= 45.093 MUR= 0.012937 EMC= 0.00000863 
+ GCO=-0.5 GCF= 0.00012 
+ CGA=1.60E-12 CGK=2.30E-12 CAK=4.00E-13
.ENDS ECC81
*$
*
*
**********************************************************************
* GENERIC: 12AX7 / ECC83
* MODEL:   12AX7
* NOTES:   Heater model for one half of heater (6.3V)
**********************************************************************
.SUBCKT ECC83 A G K H1 H2
XV1 A G K H1 H2 TRIODE
+PARAMS: RCO= 6.2 RHO= 42 HTV= 6.3 HWU= 10.5 
+ LIP= 1.5 LIF= 0.000016 RAF= 0.076498 RAS= 1 CDO=-0.53056 
+ RAP= 0.18 ERP= 1.5 
+ MU0= 87.302 MUR=-0.013621 EMC= 0.00000111 
+ GCO=-0.2 GCF= 0.00001 
+ CGA=3.90E-12 CGK=2.40E-12 CAK=7.00E-13
.ENDS ECC83
*$
*
*
************************************************
* Filename:		6bq5.inc  12/10/97  V3
* Simulator:		PSpice
* Device type:		Power pentode
* Device model:		6BQ5/EL84
*
* Author:		Duncan Munro
* Date:			12/5/97
* Copyright:		(C)1997 DDS
*
*
* V3 [12/10/97]: Screen current limited to prevent screen current
*		 draw at Vs = 0.
* 
* The following parameters are not modelled:
*
*   (1) Heater
*   (2) Grid current is an approximation
*
* Please note that this model is provided "as is" and
* no warranty is provided in respect of its suitability
* for any application.
*
* This model is provided for educational and non-profit use.
*
* Email queries to duncan@muffy.demon.co.uk
*
* Pins   A  Anode
*        S  Screen
*        G  Grid
*        K  Cathode
*
*
.SUBCKT EL84 A S G K
*
* Calculate contribution to cathode current
*
Eat	at	0	VALUE={0.636*ATAN(V(A,K)/15)}
Egs	gs	0	VALUE={LIMIT{V(S,K)/19+V(G,K)+V(A,K)/1400,0,1E6}}
Egs2	gs2	0	VALUE={PWRS(V(gs),1.5)}
Ecath  	cc    	0 	VALUE={V(gs2)*V(at)}
*
* Calculate anode current
*
Ga     	A     	K     	VALUE={3.2E-3*V(cc)}
*
* Calculate screen current
*
Escrn	sc	0	VALUE={V(gs2)*(1.1-V(at))}
Gs    S     K	VALUE={2.0E-3*V(sc)*LIMIT{V(S,K),0,10}/10}
*
* Grid current (approximation - does not model low va/vs)
*
Gg	G	K	VALUE={PWR(LIMIT{V(G,K)+1,0,1E6},1.5)*50E-6}
*
* Capacitances
*
Cg1	G	K	10.8p
Cak	A	K	6.5p
Cg1a	G	A	0.5p
*
.ENDS EL84
*$
*
*******************************************************
* Transformateur d'alimentation secteur 220V
*
* Subcircuit for Parmeko mains transformer model 6000/71
*
* Primary 230/240V AC
*
* Secondary 318V AC @ 300mA DC
*           6.3V AC @ 5A
*
* D.Munro - 10/4/97
*
* 10/04/97  Initial model
*
* 02/05/97  Model changed to take out leaked inductance/capacitance etc.
*           Also "leak" resistors put in to stop error messages when windings
*           were left unconnected.
*
*
* Pin 1   Mains neutral
*     2   Mains 230V tap
*     3   Mains 240V tap
*
*     4,5 318V AC
*     6,7 6.3V AC
*
*               || .--- [4]     			
* [1] Neutre--. || (
*             ) || (   318V
*             ) || (
*             ) || .--- [5]    
* [2] 230V----. || (
*             ) || .--- [6]
*             ) || (
*             ) || (   6,3V
* [3] 240V----. || (
*               || .--- [7]
*
.SUBCKT Transfo_220 1 2 3 4 5 6 7
*
* Primary
*
Rpri1   1       10      5.7
Lpri1   10      2       5.1H
Rpri2   2       11      0.5
Lpri2   11      3       10mH
Rleak1  0       1       1000MEG
Rleak2  0       2       1000MEG
Rleak3  0       3       1000MEG
*
* HV Secondary
*
Rsec1   4       20      18
Lsec1   20      21      4.8H
Rsec2   21      22      7.5
Lsec2   22      5       1.17H
Rleak4  0       4       1000MEG
Rleak5  0       5       1000MEG
*
* Heater secondary
*
Rsec3   6       30      0.025
Lsec3   30      31      1.09mH
Rsec4   31      32      0.025
Lsec4   32      7       1.09mH
Rleak6  0       6       1000MEG
Rleak7  0       7       1000MEG
*
* Coupling
*
K1      Lpri1 Lpri2 Lsec1 Lsec2 Lsec3 Lsec4 0.997
*
.ENDS Transfo_220
*$
*
****************************************************************
*
* Trace Elliot 15W output transformer, 8k primary, 16/8 ohm sec.
* Part no. 73 TRAN 15W OP
*
* [1] Red   ---. ||
*              ) || .--- Green (16 ohm)  [4]
*              ) || (
*              ) || (
* [2] White ---. || .--- Yellow (8 ohm)  [5]
*              ) || (
*              ) || (
*              ) || .--- Brown           [6]
* [3] Blue  ---. ||
*
.SUBCKT Transfo_EL84 1 2 3 4 5 6
*
* Primary
*
Lleak1  1       20      2mH
Lpri1   20      21      150H
Rpri1   21      2       186
Cpri1   1       2       120p
Lleak2  2       22      2mH
Lpri2   22      23      150H
Rpri2   23      3       201
Cpri2   2       3       120p
*
* Secondary
*
Lleak3  5       24	10uH
Lsec1   24      25      0.6H
Rsec1   25      6       0.8
Lleak4  4	    27	10uH
Lsec2	  27      28	0.3H
Rsec2	  28	    5	      0.4
*
Kcoup   Lpri1 LPri2 Lsec1 Lsec2 1.0
*
.ENDS Transfo_EL84
*$
*
* --------------------------------
* fin de la librairie Ampli a TUBE
* --------------------------------
*
.SUBCKT 4011B   3   5   8   9
*Connections   e1  e2   s  Vcc
M2 1 3 0 0 CD4011BN
M15 6 5 9 9 CD4011BP
M5 6 5 0 0 CD4011BN
M16 8 11 9 9 CD4011BP
M7 8 11 0 0 CD4011BN
M17 11 6 4 9 CD4011BP
M9 11 6 0 0 CD4011BN
M14 1 3 9 9 CD4011BP
M12 11 1 0 0 CD4011BN
M13 4 1 9 9 CD4011BP
*
.MODEL CD4011BN NMOS (LEVEL=1 VTO=3.5 KP=2.9M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
+ CBD=21.2P CBS=25.4P CGSO=31.2N CGDO=26N CGBO=42.8N)
.MODEL CD4011BP PMOS (LEVEL=1 VTO=-3.0 KP=2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=145.2 IS=31.2F PB=.8 MJ=.46
+ CBD=15.9P CBS=19P CGSO=23.4N CGDO=19.5N CGBO=32.1N)
.ENDS 4011B
*$
*
************************
.MODEL NMOS_4xxx NMOS (LEVEL=1 VTO=3.5 KP=2.9M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
+ CBD=21.2P CBS=25.4P CGSO=31.2N CGDO=26N CGBO=42.8N)
*$
.MODEL PMOS_4xxx PMOS (LEVEL=1 VTO=-3.0 KP=2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=145.2 IS=31.2F PB=.8 MJ=.46
+ CBD=15.9P CBS=19P CGSO=23.4N CGDO=19.5N CGBO=32.1N)
*
*********************************
*
* (c) Loutre Noel mai 2001
*
*$
.SUBCKT 4023B e1 e2 e3 s Vcc
*
* S = /e1 + /e2 + /e3
*
M_M5         Vcc e3 3 Vcc PMOS_4xxx           
M_M10         6 1 0 0 NMOS_4xxx           
M_M11         6 2 0 0 NMOS_4xxx           
M_M12         6 3 0 0 NMOS_4xxx           
M_M7         Vcc 1 4 Vcc PMOS_4xxx           
M_M9         5 3 6 Vcc PMOS_4xxx           
M_M8         4 2 5 Vcc PMOS_4xxx           
M_M13         s 6 0 0 NMOS_4xxx           
M_M14         Vcc 6 s Vcc PMOS_4xxx           
M_M3         Vcc e2 2 Vcc PMOS_4xxx           
M_M1         Vcc e1 1 Vcc PMOS_4xxx           
M_M2         1 e1 0 0 NMOS_4xxx           
M_M4         2 e2 0 0 NMOS_4xxx           
M_M6         3 e3 0 0 NMOS_4xxx 
*
.MODEL NMOS_4xxx NMOS (LEVEL=1 VTO=3.5 KP=2.9M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
+ CBD=21.2P CBS=25.4P CGSO=31.2N CGDO=26N CGBO=42.8N)
.MODEL PMOS_4xxx PMOS (LEVEL=1 VTO=-3.0 KP=2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=145.2 IS=31.2F PB=.8 MJ=.46
+ CBD=15.9P CBS=19P CGSO=23.4N CGDO=19.5N CGBO=32.1N)
.ENDS 4023B
*$
*
*******************************
*
* (c) Loutre Noel mai 2001
*
.SUBCKT 4012B e1 e2 e3 e4 s Vcc
*
* S = /e1 + /e2 + /e3 /e4
*
M_M7         Vcc 1 5 Vcc PMOS_4xxx           
M_M8         5 2 6 Vcc PMOS_4xxx           
M_M2         1 e1 0 0 NMOS_4xxx           
M_M1         Vcc e1 1 Vcc PMOS_4xxx           
M_M3         Vcc e2 2 Vcc PMOS_4xxx           
M_M4         2 e2 0 0 NMOS_4xxx           
M_M9         6 3 7 Vcc PMOS_4xxx           
M_M6         3 e3 0 0 NMOS_4xxx           
M_M5         Vcc e3 3 Vcc PMOS_4xxx           
M_M17         4 e4 0 0 NMOS_4xxx           
M_M18         Vcc e4 4 Vcc PMOS_4xxx           
M_M11         8 2 0 0 NMOS_4xxx           
M_M10         8 1 0 0 NMOS_4xxx           
M_M13         s 8 0 0 NMOS_4xxx           
M_M14         Vcc 8 s Vcc PMOS_4xxx           
M_M16         7 4 8 Vcc PMOS_4xxx           
M_M12         8 3 0 0 NMOS_4xxx           
M_M15         8 4 0 0 NMOS_4xxx
.ENDS 4012B
*$
*
*****************
*
* 
.SUBCKT   4001B  6   4   9   5    
*Connections    e1  e2   s  Vcc 
*CMOS Nor Gate 2 input
*
M11 1 6 5 5 CD4001BP
M3 2 4 0 0 CD4001BN
M12 2 4 5 5 CD4001BP
M5 3 1 8 8 CD4001BN
M13 3 2 5 5 CD4001BP
M7 9 3 0 0 CD4001BN
M14 9 3 5 5 CD4001BP
M15 3 1 5 5 CD4001BP
M10 8 2 0 0 CD4001BN
M1 1 6 0 0 CD4001BN
.MODEL CD4001BN NMOS (LEVEL=1 VTO=3.5 KP=2.9M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
+ CBD=21.2P CBS=25.4P CGSO=31.2N CGDO=26N CGBO=42.8N)
.MODEL CD4001BP PMOS (LEVEL=1 VTO=-3.0 KP=2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=145.2 IS=31.2F PB=.8 MJ=.46
+ CBD=21.2P CBS=25.4P CGSO=31.2N CGDO=26N CGBO=42.8N)
.ENDS 4001B
*$
*
***********************
*
.SUBCKT 4016B 1 2 3 4
* Specifications from SGS and Harris data books
* 1 ANALOG INPUT
* 2 ANALOG OUTPUT
* 3 CONTROL
* 4 Vcc (POSITIVE SUPPLY)
* 0 GND (NEGATIVE SUPPLY)
*
RINP  3 11 1500
D1 11 4 D1
D2 5 11 D1
*
M1 12 11 4 4 MP4016A L=8U W=48U  AD=500P AS=500P PD=110U PS=110U
M2 12 11 0 0 MN4016A L=8U W=16U  AD=160P AS=160P PD=48U PS=48U
*
M1A 13 12 4 4 MP4016A L=8U W=96U  AD=500P AS=500P PD=110U PS=110U
M2A 13 12 0 0 MN4016A L=8U W=32U  AD=160P AS=160P PD=48U PS=48U
*
M3 1 12 2 4  MP4016A L=8U W=360U AD=3600P AS=3600P PD=900U PS=900U
M4 2 13 1 0  MN4016A L=8U W=120U AD=1080P AS=1080P PD=286U PS=286U
*
.MODEL MN4016A NMOS LEVEL=2 VTO=1.45 TOX=1000E-10 NSUB=4.7E16
+XJ=3U LD=2U UO=625 UCRIT=1E5 UEXP=0.45 UTRA=0.25 RSH=15 
+NEFF=2.5 VMAX=1E6 CGBO=3E-10 CGDO=10E-10 CGSO=10E-10 CJSW=2F
+NFS=4E12  GAMMA=2.2 LAMBDA=0.02
*
.MODEL MP4016A PMOS LEVEL=2 VTO=-1.5 TOX=1000E-10 NSUB=7.6E15
+XJ=3U LD=1.5U UO=225 UCRIT=3E5 UEXP=.5 UTRA=0.25 RSH=15
+NEFF=2.5 VMAX=1E5 CGBO=3E-10 CGDO=10E-10 CGSO=10E-10 CJSW=1F
+NFS=1E12  GAMMA=1.8 LAMBDA=0.02
* 
.MODEL D1 D IS=923.17E-18 RS=10 CJO=1.0000E-12 M=.3333  VJ=.75
+ ISR=100.00E-12  BV=35.357  IBV=10U  TT=5.0000E-9
.ENDS 4016B
*$
*
*************************************************
.SUBCKT 4050B  1  2   3  
*Hex Buffer    e  s  Vcc
M1 4 1 3 3 CD4069BP
M2 4 1 0 0 CD4069BN
M3 2 4 3 3 CD4069BP
M4 2 4 0 0 CD4069BN
.MODEL CD4069BN NMOS (LEVEL=1 VTO=2.1 KP=2.9M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
+ CBD=47.6P CBS=57.2P CGSO=70.2N CGDO=58.5N CGBO=96.3N)
.MODEL CD4069BP PMOS (LEVEL=1 VTO=-2.9 KP=2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=145.2 IS=31.2F PB=.8 MJ=.46
+ CBD=47.6P CBS=57.2P CGSO=70.2N CGDO=58.5N CGBO=96.3N)
*****
.MODEL CD4049P PMOS (LEVEL=1 VTO=-2.9 KP=2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=45.2 IS=31.2F PB=.8 MJ=.46
+ CBD=148P CBS=177P CGSO=218N CGDO=182N CGBO=299N)
.MODEL CD4049N NMOS (LEVEL=1 VTO=2.1 KP=5M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=4.2 RS=4.2 IS=31.2F PB=.8 MJ=.46
+ CBD=105P CBS=127P CGSO=156N CGDO=130N CGBO=214N)
.ENDS 4050B
*$
*********
.SUBCKT 4069UB  6  1  4   
*Connections    e  s  Vcc
M2 1 6 0 0 CD4069BN
M3 1 6 4 4 CD4069BP
.MODEL CD4069BN NMOS (LEVEL=1 VTO=2.1 KP=2.9M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
+ CBD=47.6P CBS=57.2P CGSO=70.2N CGDO=58.5N CGBO=96.3N)
.MODEL CD4069BP PMOS (LEVEL=1 VTO=-2.9 KP=2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=145.2 IS=31.2F PB=.8 MJ=.46
+ CBD=47.6P CBS=57.2P CGSO=70.2N CGDO=58.5N CGBO=96.3N)
.ENDS 4069UB
*$
*
*************
.SUBCKT 4049UB 1  2  3 
*Hex Buffer    e  s Vcc
M1 2 1 3 3 CD4049P
M2 2 1 0 0 CD4049N
.MODEL CD4049P PMOS (LEVEL=1 VTO={VTO_PMOS} KP=2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=45.2 IS=31.2F PB=.8 MJ=.46
+ CBD=148P CBS=177P CGSO=218N CGDO=182N CGBO=299N)
.MODEL CD4049N NMOS (LEVEL=1 VTO={VTO_NMOS} KP=5M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=4.2 RS=4.2 IS=31.2F PB=.8 MJ=.46
+ CBD=105P CBS=127P CGSO=156N CGDO=130N CGBO=214N)
.ENDS 4049UB
*$
*
*************************************
.SUBCKT 4069B  e  s  Vcc
*
X1   1   s   Vcc   4050B
X2   e   1   Vcc   4069UB
*
.ENDS 4069B
*$
*
*
*
.SUBCKT 4093B  3   2  13   9
*Connections   e1  e2  S  Vcc
X1 15 1 9 0 INV4093
X2 1 17 9 0 INV4093 
X3 1 13 9 0 INV4093
M12 7 11 0 0 CD4093N L=.6U W=2U
M13 15 12 7 0 CD4093N L=.6U W=2U
M16 5 11 17 0 CD4093N L=.4U W=.7U
M17 15 12 5 0 CD4093N L=.4U W=.7U
M18 15 11 9 9 CD4093P L=.4U W=.8U
M19 15 12 9 9 CD4093P L=.4U W=.8U
M20 15 12 17 9 CD4093P L=1.4U W=1.4U
M21 15 11 17 9 CD4093P L=1.4U W=1.4U
R1 3 12 .1
R2 2 11 .1
.MODEL CD4093N NMOS (LEVEL=1 VTO=2.1 KP=3M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.97M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
+ CBD=15.9P CBS=19P CGSO=23.4N CGDO=19.5N CGBO=32.1N)
.MODEL CD4093P PMOS (LEVEL=1 VTO=-1.9 KP=2.2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.97M RD=28.2 RS=145.2 IS=31.2F PB=.8 MJ=.46
+ CBD=15.9P CBS=19P CGSO=23.4N CGDO=19.5N CGBO=32.1N)
*
.ENDS 4093B
*$
*
.SUBCKT INV4093 6  1   4    0 
*Connections    e  S  Vcc  GND
M2 1 6 0 0 CD469BN
M3 1 6 4 4 CD469BP
.MODEL CD469BN NMOS (LEVEL=1 VTO=2.1 KP=3M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
* CBD=15.9P CBS=19P CGSO=23.4N CGDO=19.5N CGBO=32.1N)
+ CBD=21.2P CBS=25.4P CGSO=31.2N CGDO=26N CGBO=42.8N)
.MODEL CD469BP PMOS (LEVEL=1 VTO=-1.9 KP=2.2M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=28.2 RS=145.2 IS=31.2F PB=.8 MJ=.46
* CBD=15.9P CBS=19P CGSO=23.4N CGDO=19.5N CGBO=32.1N)
+ CBD=21.2P CBS=25.4P CGSO=31.2N CGDO=26N CGBO=42.8N)
.ENDS INV4093 
*$
*
***************
*
.MODEL NMOS_vco_in NMOS (LEVEL=1 VTO=1.5 KP=2.9M GAMMA=3.97U
+ PHI=.75 LAMBDA=1.87M RD=20.2 RS=184.1 IS=31.2F PB=.8 MJ=.46
+ CBD=21.2P CBS=25.4P CGSO=31.2N CGDO=26N CGBO=42.8N)
*
******************************
*** N-Channel 160V 7a
*$
* lib de Micro-Cap v7.0.X
.MODEL 2SK1058_mc7 NMOS (VTO=-767.135M KP=20U L=2U W=20.8177M GAMMA=0 PHI=600M
+ LAMBDA=862.511U CBD=915.478P IS=10F CGSO=1.65276N CGDO=1.65276N TOX=0 NSUB=0
+ TPG=1 UO=600 RG=10 RDS=1MEG GDSNOI=0 T_ABS={temp_abs})
*
*$
